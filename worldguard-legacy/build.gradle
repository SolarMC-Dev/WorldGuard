import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'eclipse'
apply plugin: 'idea'

ext.internalVersion = project.version + ";" + gitCommitHash

dependencies {
    implementation 'org.khelekore:prtree:1.5.0'
    implementation "gg.solarmc.paper:solarpaper-api:1.2.0"
    implementation ("com.google.guava:guava") {
        version {
            strictly "21.0"
        }
    }
    implementation "gg.solarmc.worldedit:worldedit-bukkit:6.1.9"
    implementation 'com.sk89q:squirrelid:0.1.0'
    implementation 'org.flywaydb:flyway-core:3.0'
    implementation ('com.sk89q:commandbook:2.3') {
        exclude group: "org.bukkit", module: "bukkit"
        exclude group: 'com.sk89q', module: 'worldedit'
        exclude group: 'com.zachsthings.libcomponents'
    }
    implementation 'net.sf.opencsv:opencsv:2.0'
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    implementation 'com.google.code.findbugs:jsr305:1.3.9'
    testImplementation 'junit:junit:4.11'
    testImplementation 'org.hamcrest:hamcrest-library:1.2.1'
}

processResources {
    filter ReplaceTokens, tokens: [
        "project.internalVersion" : project.internalVersion
    ]
}

jar {
    manifest {
        attributes("Automatic-Module-Name" : "com.sk89q.worldguard")
   }
}

shadowJar {
    dependencies {
        include(dependency('org.khelekore:prtree:1.5.0'))
        include(dependency('com.sk89q:squirrelid:0.1.0'))
        include(dependency('org.flywaydb:flyway-core:3.0'))
        include(dependency('com.googlecode.json-simple:json-simple:1.1.1'))
        include(dependency('net.sf.opencsv:opencsv:2.0'))
    }

    relocate('org.flywaydb', 'com.sk89q.worldguard.internal.flywaydb')
    relocate('com.sk89q.squirrelid', 'com.sk89q.worldguard.util.profile')
    relocate('org.json.simple', 'com.sk89q.worldguard.util.jsonsimple')

}

build.dependsOn(shadowJar)
